// TicTacToe:

// Roles

(role x)
(role o)

// Types

(type mark x)
(type mark o)
(type mark b)


// Initial State

(init (cell 1 1 b))
(init (cell 1 2 b))
(init (cell 1 3 b))
(init (cell 2 1 b))
(init (cell 2 2 b))
(init (cell 2 3 b))
(init (cell 3 1 b))
(init (cell 3 2 b))
(init (cell 3 3 b))
(init (control x))


// Dynamic Components:

// Cell

(<= (next (cell [1,3]:?x [1,3]:?y mark:?player))
    (does ?player (mark ?x ?y))
)

(<= (next (cell [1,3]:?x [1,3]:?y mark:?mark))
    (true (cell ?x ?y ?mark))
    (does ?player (mark ?m ?n))
    (distinctCell ?x ?y ?m ?n)
)

// Control

(<= (next (control x))
    (true (control o))
)

(<= (next (control o))
    (true (control x))
)


// Views:

(<= (row ?x ?player)
    (true (cell ?x 1 ?player))
    (true (cell ?x 2 ?player))
    (true (cell ?x 3 ?player))
)

(<= (column ?y ?player)
    (true (cell 1 ?y ?player))
    (true (cell 2 ?y ?player))
    (true (cell 3 ?y ?player))
)

(<= (diagonal ?player)
    (true (cell 1 1 ?player))
    (true (cell 2 2 ?player))
    (true (cell 3 3 ?player))
)

(<= (diagonal ?player)
    (true (cell 1 3 ?player))
    (true (cell 2 2 ?player))
    (true (cell 3 1 ?player))
)

(<= (line ?player)
    (row ?x ?player)
)

(<= (line ?player)
    (column ?y ?player)
)

(<= (line ?player)
    (diagonal ?player)
)

(<= (open)
    (true (cell ?x ?y b))
)

(<= (distinctCell ?x ?y ?m ?n)
    (distinct ?x ?m)
)
(<= (distinctCell ?x ?y ?m ?n)
    (distinct ?y ?n)
)


// Legal Moves

(<= (legal ?player (mark [1,3]:?x [1,3]:?y))
    (true (cell ?x ?y b))
    (true (control ?player))
)


// Goals:

(<= (goal ?player 100)
    (line ?player)
    (role ?player)
)

(<= (goal ?player 50)
    (not (line x))
    (not (line o))
    (not (open))
    (role ?player)
)

(<= (goal ?player1 0)
    (line ?player2)
    (distinct ?player1 ?player2)
    (role ?player1)
    (role ?player2)
)

(<= (goal ?player 0)
    (not (line x))
    (not (line o))
    (open)
    (role ?player)
)


// Terminal:

(<= terminal
    (line ?player)
    (role ?player)
)

(<= terminal
    (not (open))
)
