/* (c) https://github.com/MontiCore/monticore */
// transportation problem example (linear)
// see https://www.gams.com/products/simple-example/

package de.rwth.monticar.optimization;

component TransportationProblem{

    ports out Q^{3, 2} xOut,
          out Q yOut;

    implementation Math
    {
        // define problem
        Q m = 2;
        Q n = 3;

        // define A, b
        Q^{2, 1} A = [350; 600];
        Q^{3, 1} b = [325; 300; 275];

        // cost matrix
        Q ^{m, n} c = [2.5, 1.7, 1.8; 2.5, 1.8, 1.4];

        // minimization problem
        minimize
            Q^{2, 3} x;
        in
            Q y = sum(c .* x);
        subject to
            sum(x, 2) == A;
            sum(x, 1) == b;
            x >= 0;
        end
        xOut = x;
        yOut = y;
    }
}
