/* (c) https://github.com/MontiCore/monticore */
// PathPlaner:
// - First trims path to current progress
// - Then creates low level reference state vector for mpc controller
// @author Christoph Richter

package de.rwth.monticar.mpc.trajectory;

component PathPlaner<Q dt = 0.1>
{
    ports
        // state vector z
        in Q                                        x,
        in Q                                        y,
        in Q                                        yaw,
        in Q (0.0 m/s : 0.01 m/s : oo m/s)          v,

        // planned trajectory
        in  Z (0 : 100)                             trajectory_length,
        in  Q (-oo m : 0.01 m : oo m) ^ {1,100}     trajectory_x,
        in  Q (-oo m : 0.01 m : oo m) ^ {1,100}     trajectory_y,

        // reference trajectory for prediction horizon
        out Q^{4, 5}                                   z_ref;

    // trims planned trajectory to current position
    instance TrimPath pathTrimmer;

    // creates a low lever reference path for the prediction horizon
    instance ReferencePathCreator referencePathCreator;

    // velocity controller
    instance VelocityController velocityController;

    // connect trim path
    connect x -> pathTrimmer.currentPositionX;
    connect y -> pathTrimmer.currentPositionY;
    connect trajectory_length -> pathTrimmer.plannedTrajectoryLength;
    connect trajectory_x -> pathTrimmer.plannedTrajectoryX;
    connect trajectory_y -> pathTrimmer.plannedTrajectoryY;

    // connect referencePathCreator
    connect x -> referencePathCreator.x;
    connect y -> referencePathCreator.y;
    connect velocityController.desiredVelocity -> referencePathCreator.desiredVelocity;

    connect pathTrimmer.trimmedTrajectoryLength -> referencePathCreator.trajectory_length;
    connect pathTrimmer.trimmedTrajectoryX -> referencePathCreator.trajectory_x;
    connect pathTrimmer.trimmedTrajectoryY -> referencePathCreator.trajectory_y;

    // connect output
    connect referencePathCreator.z_ref -> z_ref;
}
