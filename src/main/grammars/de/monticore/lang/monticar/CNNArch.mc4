package de.monticore.lang.monticar;

grammar CNNArch extends de.monticore.CommonExpressions, de.monticore.lang.Math, de.monticore.lang.monticar.Common2 {

    token NEWLINETOKEN =
                 ('\r' '\n' |
                 '\r' |
                 '\n' ):;

    /* =================================*/
    /* ========== PRODUCTIONS ==========*/
    /* =================================*/

    /* ========== Declarations =========*/

    /**
        The complete file.
        Use nonterminal Architecture for embedding in another language (e.g. EmbeddedMontiArc)
    */
    symbol scope CNNArchCompilationUnit = "architecture"
                                          name:Name
                                          ( "(" (ArchitectureParameter || ",")* ")" )? "{"
                                          ioDeclarations:IODeclaration*
                                          Architecture
                                          "}";

    LayerDeclaration = NEWLINETOKEN* "def"
                       Name "("
                       parameters:(LayerParameter || ",")* ")" "{" NEWLINETOKEN*
                       body:ArchBody NEWLINETOKEN* "}";

    IODeclaration = NEWLINETOKEN* "def"
                    (in:"input" | out:"output")
                    type:ArchType
                    Name
                    (ArrayDeclaration)?;


    /* ============== Type =============*/

    /**
         Similar to EmbeddedMontiArc port types.
         ArchType and Shape are not used if the Architecture is integrated into EmbeddedMontiArc
    */
    ArchType = ElementType "^" Shape;

    Shape = "{" dimensions:(ArchSimpleExpression || ",")* "}";


    /* ========= Architecture  =========*/

    /**
        Defines the architecture of the neural network.
        This NT is used for integration in EmbeddedMontiArc.
        @attribute methodDeclaration*
                 A list of new layers which can be used in the architecture.
        @attribute body
                 The architecture of the neural network.
    */
    Architecture = NEWLINETOKEN* methodDeclaration:LayerDeclaration* NEWLINETOKEN*
                   body:ArchBody NEWLINETOKEN*;

    scope ArchBody = elements:(ArchitectureElement || "->")*;

    interface ArchitectureElement;

    IOElement implements ArchitectureElement = NEWLINETOKEN* Name ("[" index:ArchSimpleExpression "]")?;

    Layer implements ArchitectureElement = NEWLINETOKEN* Name "(" arguments:(ArchArgument || ",")* ")";

    ParallelBlock implements ArchitectureElement = NEWLINETOKEN* "(" NEWLINETOKEN*
                                                   groups:ArchBody NEWLINETOKEN* "|" NEWLINETOKEN*
                                                   groups:(ArchBody || "|")+ NEWLINETOKEN* ")";

    ArrayAccessLayer implements ArchitectureElement = NEWLINETOKEN* "[" index:ArchSimpleExpression "]";


    /* ====== Variables/Arguments ======*/

    interface Variable;

    ArchitectureParameter implements Variable = NEWLINETOKEN* Name ("=" default:ArchSimpleExpression)? NEWLINETOKEN*;

    LayerParameter implements Variable = NEWLINETOKEN* Name ("=" default:ArchSimpleExpression)? NEWLINETOKEN*;

    interface ArchArgument;

    ArchParameterArgument implements ArchArgument = NEWLINETOKEN* Name "=" rhs:ArchExpression NEWLINETOKEN*;

    ArchSpecialArgument implements ArchArgument = NEWLINETOKEN*
                                                  (serial:"->" | parallel:"|" | conditional:"?") "="
                                                  rhs:ArchExpression NEWLINETOKEN*;


    /* ======= Value Expressions =======*/

    /**
        Expression used for method arguments.
    */
    ArchExpression = (expression:ArchSimpleExpression | sequence:ArchValueSequence);

    interface ArchValueSequence;

    ArchParallelSequence implements ArchValueSequence = "[" parallelValues:(ArchSerialSequence || "|")+ "]";

    ArchSerialSequence = serialValues:(ArchSimpleExpression || "->")*;

    ArchValueRange implements ArchValueSequence = "[" start:ArchSimpleExpression
                                                  (serial:"->" | parallel:"|")
                                                  ".."
                                                  (serial2:"->" | parallel2:"|")
                                                  end:ArchSimpleExpression "]";
    /**
        Expressions for variable values.
    */
    ArchSimpleExpression = (arithmeticExpression:ArchArithmeticExpression
                         | booleanExpression:ArchBooleanExpression
                         | tupleExpression:TupleExpression
                         | string:StringLiteral);

    interface ArchMathExpression extends Expression;
    interface ArchArithmeticExpression extends ArchMathExpression;
    interface ArchBooleanExpression extends ArchMathExpression;

    ArchSimpleArithmeticExpression implements ArchArithmeticExpression = (NumberExpression
                                             | NameExpression
                                             | MathDottedNameExpression
                                             | MathAssignmentDeclarationStatement
                                             | MathAssignmentStatement);

    ArchComplexArithmeticExpression implements ArchArithmeticExpression = leftExpression:ArchMathExpression
                                               NEWLINETOKEN*
                                               (   operator:"*"
                                               |   operator:"/"
                                               |   operator:"%"
                                               |   operator:"^"
                                               |   operator:"+"
                                               |   operator:"-"
                                               )
                                               NEWLINETOKEN*
                                               rightExpression:ArchMathExpression;

    TupleExpression = "(" expressions:ArchArithmeticExpression "," expressions:(ArchArithmeticExpression || ",")* ")";

    ArchSimpleBooleanExpression implements ArchBooleanExpression = (BooleanExpression
                         | BooleanNotExpression
                         | LogicalNotExpression);

    ArchComplexBooleanExpression implements ArchBooleanExpression = leftExpression:ArchMathExpression
                                                                         (   operator:"=="
                                                                         |   operator:"!="
                                                                         |   operator:"&&"
                                                                         |   operator:"||"
                                                                         )
                                                                         rightExpression:ArchMathExpression;

    ArchBracketExpression implements ArchMathExpression, ArchBooleanExpression, ArchArithmeticExpression = "(" ArchMathExpression ")";

    ArchPreMinusExpression implements ArchMathExpression, ArchBooleanExpression, ArchArithmeticExpression = "-" ArchMathExpression ;


    /* =================================*/
    /* ============ ASTRULES ===========*/
    /* =================================*/
    ast Variable = method String getName(){};
    ast ArchSpecialArgument = method public String getName(){return "";}; //Override is necessary
    ast ArchArgument = method String getName(){}
                       method ASTArchExpression getRhs(){};

}