/* (c) https://github.com/MontiCore/monticore */


package de.monticore.lang.embeddedmontiarcdynamic;


grammar EmbeddedMontiArcDynamic extends de.monticore.lang.embeddedmontiarcdynamic.Event, de.monticore.lang.embeddedmontiarc.EmbeddedMontiArc, de.monticore.lang.monticar.Common2{

    start EMACompilationUnit;

    // Extend Component for dynamic component. [isDynamic is new]
    Component =
            (dynamic:["dynamic"])? "component" (["interface"])? Name
            genericTypeParameters:TypeParameters2?
            (  "("  (Parameter || ",")+  ")" )?
            ("implements" superComponent:ReferenceType)?
            body:ComponentBody;




    // Handler for events
    scope EventHandler implements Element = (EventReferenceExpression | "@" Expression) "{"
        body:Element*
     "}" ;

    // Dynamic Port(s) [isDynamic is new]
    //
    Port =
          (dynamic:["dynamic"])?
          AdaptableKeyword? (incoming:["in"] | outgoing:["out"])
          Type (Name? | Name ( "[" UnitNumberResolution (":" dynamicNumberOfPorts:UnitNumberResolution)? "]" )?);


    // Dynamic SubComponent(s)

    // SubComponentInstance extension
    // Dynamic number
    SubComponentInstance =
        Name
        ("[" UnitNumberResolution (":" dynamicNumberOfInstances:UnitNumberResolution)? "]")?;

    //QualifiedNameWithArray =
    //        (dynamicNewComponent:NewDynamicIndicator? compName:Name compArray:ArrayAccess? ".")? (dynamicNewPort:NewDynamicIndicator? portName:Name portArray:ArrayAccess?  );
    ArrayAccess = ("[" ( intLiteral:UnitNumberResolution |
                    [":"] |
                    lowerbound:UnitNumberResolution ":" upperbound:UnitNumberResolution |
                    dynamicNewPort:["?"] ) "]" );

//    AvailableDynamicPort implements Expression <200>  =
//                "available" "(" portName:QualifiedNameWithArray ")";
}
