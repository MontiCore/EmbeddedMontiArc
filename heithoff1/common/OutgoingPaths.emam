package de.rwth.pacman.heithoff1.common;

import de.rwth.pacman.common.BoolToNumber;
import de.rwth.pacman.common.Sum;

component OutgoingPaths {
    ports
        in Q(-1m: 19m) pacManX,
        in Q(0m: 22m) pacManY,
        in Z^{22,19} map,
        
        out Boolean isTopFree,
        out Boolean isBottomFree,
        out Boolean isLeftFree,
        out Boolean isRightFree,
        out Z outgoingPaths;

    instance IsDirectionFree<-0.51, 0> topFree;
    instance IsDirectionFree<0.51, 0> bottomFree;
    instance IsDirectionFree<0, -0.51> leftFree;
    instance IsDirectionFree<0, 0.51> rightFree;

    connect pacManX -> topFree.indexX, bottomFree.indexX, leftFree.indexX, rightFree.indexX;
    connect pacManY -> topFree.indexY, bottomFree.indexY, leftFree.indexY, rightFree.indexY;
    connect map -> topFree.map, bottomFree.map, leftFree.map, rightFree.map;
    
    instance BoolToNumber boolToNumber1, boolToNumber2, boolToNumber3, boolToNumber4;
    
    connect topFree.isDirFree -> isTopFree, boolToNumber1.in1;
    connect bottomFree.isDirFree -> isBottomFree, boolToNumber2.in1;
    connect leftFree.isDirFree -> isLeftFree, boolToNumber3.in1;
    connect rightFree.isDirFree -> isRightFree, boolToNumber4.in1;

    instance Sum<4> sum;
    
    connect boolToNumber1.out1 -> sum.in1[1];
    connect boolToNumber2.out1 -> sum.in1[2];
    connect boolToNumber3.out1 -> sum.in1[3];
    connect boolToNumber4.out1 -> sum.in1[4];

    connect sum.out1 -> outgoingPaths;
}