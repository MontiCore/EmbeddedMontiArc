package de.rwth.armin.modeling.autopilot.common;

component SignedAngleBetween {
  port
    in  Q ^2 v1,
    in  Q ^2 v2,

    out Q angle;

  implementation Math {
    static Q EPSILON = 0.001;
    angle = 0.0;
    Q norm1 = sqrt( v1(1)*v1(1) + v1(2)*v1(2) );
    Q norm2 = sqrt( v2(1)*v2(1) + v2(2)*v2(2) );
    if ( (norm1 > EPSILON) && (norm2 > EPSILON) )
      Q angle1 = -atan2(v1(2), v1(1));
      Q c = cos(angle1);
      Q s = sin(angle1);
      // v2 rotated
      Q v2xr = v2(1) * c - v2(2) * s;
      Q v2yr = v2(1) * s + v2(2) * c;
      Q angle2 = -atan2(v2yr, v2xr);
      Q abs1 = abs(angle2);
      Q abs2 = abs(abs1 - M_PI);
      if (abs2 <= EPSILON)
        angle = -M_PI;
      else
        angle = angle2 * 360.0 / (2.0*M_PI);
      end
    end
  }
}
