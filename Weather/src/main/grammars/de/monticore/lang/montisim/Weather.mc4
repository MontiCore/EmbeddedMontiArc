/* 
 * advanced weather language v1.0
 * @author Deniz Schmidt
*/
package de.monticore.lang.montisim;

grammar Weather extends de.monticore.lang.NumberUnit, de.monticore.lang.monticar.Ranges {

  Coordinate = "(" posX:TUnitNumber "," posY:TUnitNumber ")";
  TUnitNumberList = "[" TUnitNumber ("," TUnitNumber)* "]";
  AlternativeInput = (TUnitNumber | TUnitNumberList | Range);

  Temperature = "temperature" AlternativeInput;

  Humidity = "humidity" AlternativeInput;

  Pressure = "pressure" AlternativeInput;

  WindStrength = "wind_strength" AlternativeInput;

  WindDirection = "wind_direction" AlternativeInput;

  PrecipitationType = "precipitation_type" precipitationType:["none"|
                                            "drizzle"|
                                            "rain"|
                                            "freezing drizzle"|
                                            "freezing rain"|
                                            "snow rain"|"snain"|
                                            "snow"|
                                            "snow_grains"|
                                            "ice_pellets"|"sleet"|
                                            "hail"|
                                            "snow_pellets"|"graupel"|
                                            "ice_crystals"] ;

  PrecipitationAmount = "precipitation_amount" AlternativeInput;
  
  Clouding = "clouding" cloudingType:["none"|
                         "cirrostratus"|
                         "altostratus"|
                         "stratus"|
                         "nimbostratus"|
                         "noctilucent"|
                         "polar_stratospheric"|
                         "cirrus"|
                         "cirrocumulus"|
                         "altocumulus"|
                         "stratocumulus"|
                         "cumulus_humilis"|
                         "cumulus_mediocris"|
                         "cumulus_congestus"|
                         "cumulonimbus"] ;

  Sight = "sight" (AlternativeInput | ["unlimited"]) ;
  
  WeatherPhenomena = "weather_phenomena" phenomenaType:["fog"|
                                           "rope_tornado"|
                                           "cone_tornado"|
                                           "wedge_tornado"|
                                           "multi-vortex_tornado"|
                                           "landspout"|
                                           "waterspout"|
                                           "gustnado"|
                                           "dust_devil"|
                                           "steam_devil"|
                                           "thunderstorm"]
                    (Coordinate)? ;
                                    
  OpticalPhenomena = "optical_phenomena" phenomenaType:["rainbow"|
                                           "northern_lights"|
                                           "circumzenithal_arc"|
                                           "zodiacal_light"|
                                           "crepuscular_rays"|
                                           "mirage"|
                                           "fog_bow"] ;
                                           
  ArtificialPhenomena = "artificial_phenomena" phenomenaType:["contrails"|
                                                 "smog"|
                                                 "rocket_exhaust_trails"] ;
}