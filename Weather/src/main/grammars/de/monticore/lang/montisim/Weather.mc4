/* 
 * advanced weather language v1.0
 * @author Deniz Schmidt
*/
package de.monticore.lang.montisim;

grammar Weather extends de.monticore.lang.NumberUnit, de.monticore.lang.monticar.Ranges {

  Coordinate = "(" posX:UnitNumber "," posY:UnitNumber ")";
  UnitNumberList = "[" UnitNumber ("," UnitNumber)* "]";
  AlternativeInput = (UnitNumber | UnitNumberList | Range);

  interface WeatherAttribute;
  interface WeatherEntity;

  Temperature implements WeatherAttribute = "temperature" AlternativeInput;

  Humidity implements WeatherAttribute = "humidity" AlternativeInput;

  Pressure implements WeatherAttribute = "pressure" AlternativeInput;

  WindStrength implements WeatherAttribute = "wind_strength" AlternativeInput;

  WindDirection implements WeatherAttribute = "wind_direction" AlternativeInput;

  PrecipitationType implements WeatherAttribute = "precipitation_type" precipitationType:["none"|
                                            "drizzle"|
                                            "rain"|
                                            "freezing_drizzle"|
                                            "freezing_rain"|
                                            "snow_rain"|"snain"|
                                            "snow"|
                                            "snow_grains"|
                                            "ice_pellets"|"sleet"|
                                            "hail"|
                                            "snow_pellets"|"graupel"|
                                            "ice_crystals"] ;

  PrecipitationAmount implements WeatherAttribute = "precipitation_amount" AlternativeInput;
  
  Clouding implements WeatherAttribute = "clouding" cloudingType:["none"|
                         "cirrostratus"|
                         "altostratus"|
                         "stratus"|
                         "nimbostratus"|
                         "noctilucent"|
                         "polar_stratospheric"|
                         "cirrus"|
                         "cirrocumulus"|
                         "altocumulus"|
                         "stratocumulus"|
                         "cumulus_humilis"|
                         "cumulus_mediocris"|
                         "cumulus_congestus"|
                         "cumulonimbus"] ;

  Sight implements WeatherAttribute = "sight" (AlternativeInput | ["unlimited"]) ;
  
  WeatherPhenomena implements WeatherEntity = "weather_phenomena" phenomenaType:["fog"|
                                           "rope_tornado"|
                                           "cone_tornado"|
                                           "wedge_tornado"|
                                           "multi-vortex_tornado"|
                                           "landspout"|
                                           "waterspout"|
                                           "gustnado"|
                                           "dust_devil"|
                                           "steam_devil"|
                                           "thunderstorm"]
                    (Coordinate)? ;
                                    
  OpticalPhenomena implements WeatherEntity = "optical_phenomena" phenomenaType:["rainbow"|
                                           "northern_lights"|
                                           "circumzenithal_arc"|
                                           "zodiacal_light"|
                                           "crepuscular_rays"|
                                           "mirage"|
                                           "fog_bow"] ;
                                           
  ArtificialPhenomena implements WeatherEntity = "artificial_phenomena" phenomenaType:["contrails"|
                                                 "smog"|
                                                 "rocket_exhaust_trails"] ;
}